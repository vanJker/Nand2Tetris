// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    // Decode Instruction
    Or16(a=false, b=instruction, out[15]=op, out[15]=Asel, out[12]=a, out[12]=ALUsel,
        out[11]=c1, out[10]=c2, out[9]=c3, out[8]=c4, out[7]=c5, out[6]=c6,
        out[5]=d1, out[4]=d2, out[3]=d3, out[2]=j1, out[1]=j2, out[0]=j3);
    
    // Data Path
    Mux16(a=instruction, b=Mval, sel=Asel, out=inA);
    // Register A
    ARegister(in=inA, load=loadA, out=outA, out[0..14]=addressM);
    Mux16(a=outA, b=inM, sel=ALUsel, out=AorM);
    // Register D
    DRegister(in=Mval, load=loadD, out=outD);
    // ALU Execution
    ALU(x=outD, y=AorM, zx=c1, nx=c2, zy=c3, ny=c4, 
        f=c5, no=c6, out=outM, out=Mval, zr=zr, ng=ng);
    // PC
    PC(in=outA, load=PCsel, inc=true, reset=reset, out[0..14]=pc);

    // Control Logic
    // IN instruction[16], zr, ng
    // OUT Asel, loadA, loadD, ALUsel, ALUop, PCsel, writeM
    Not(in=op, out=notOp);
    Or(a=d1, b=notOp, out=loadA);
    And(a=op, b=d2, out=loadD);
    Not(in=ng, out=notNg);
    Not(in=zr, out=NotZr);
    And(a=j3, b=notNg, out=andj3);
    And(a=andj3, b=NotZr, out=jgt);
    And(a=j2, b=zr, out=jeq);
    And(a=j1, b=ng, out=jlt);
    Or(a=jgt, b=jeq, out=jge);
    Or(a=jge, b=jlt, out=jmp);
    And(a=op, b=jmp, out=PCsel);
    And(a=op, b=d3, out=writeM);
}